# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=thermald
_pkgname=thermal_daemon
_ver=1.8
pkgver=1.8.0
pkgrel=1
pkgdesc="The Linux Thermal Daemon program from 01.org"
arch=('x86_64')
url="https://01.org/linux-thermal-daemon"
license=('GPL2')
makedepends=('eudev' 'python')
depends=('dbus-glib' 'libxml2')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/intel/thermal_daemon/archive/v${_ver}.tar.gz"
        'modules-load-thermald.conf')
sha256sums=('662456eff5b7ee5a42e54b8a667d002d3a4dec5cacbddc5e1ab5ec94dab218c6'
            '0155e1eb459306d251a5a049ffc6c11e144fa8caa75901ac5fa20bd52e05d515')

build() {
  cd ${_pkgname}-${_ver}
  ./autogen.sh
  ./configure  \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin
  make
}

package() {
  cd ${_pkgname}-${_ver}

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/modules-load-thermald.conf "${pkgdir}"/usr/lib/modules-load.d/thermald.conf
}
