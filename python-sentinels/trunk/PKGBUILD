# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sentinels
pkgname=(python-sentinels python2-sentinels)
pkgver=1.0.0
pkgrel=1
pkgdesc="Various objects to denote special meanings in python"
url="https://github.com/vmalloc/sentinels"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/vmalloc/sentinels/archive/$pkgver.tar.gz")
sha512sums=('80f5bc13eb902f7e8cba5a227eae3c92187d4d3203493e3e507998338c726985fe3b121e352a4235d5975bf19c3643b8120c1d15756768c268406689c2fc5489')

prepare() {
  cp -a sentinels-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sentinels-$pkgver
  python setup.py build

  cd "$srcdir"/sentinels-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sentinels-$pkgver
  python setup.py pytest

  cd "$srcdir"/sentinels-$pkgver-py2
  python2 setup.py pytest
}

package_python-sentinels() {
  depends=('python')

  cd sentinels-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sentinels() {
  depends=('python2')

  cd sentinels-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
