# Maintainer: SÃ©bastien Luttringer

pkgbase=python-psutil
pkgname=('python-psutil' 'python2-psutil')
pkgver=5.4.8
pkgrel=1
arch=('x86_64')
url='https://github.com/giampaolo/psutil'
license=('custom: BSD')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
checkdepends=('net-tools' 'procps-ng' 'python2-ipaddress' 'python2-mock' 'python-mock' 'python-pytest')
source=("https://github.com/giampaolo/psutil/archive/release-$pkgver.tar.gz"
        "fix_disk_io_counters.patch::https://github.com/giampaolo/psutil/commit/8f99f3782663959062ee868bbfdbc336307a3a4d.patch"
        "fix_test_disk_io_counters_sysfs.patch")
sha512sums=('66990d006680142c309874624882ba7612cdef99f44cfed07ba5371ba20e1c978e42364248dba09fce053d78a62630061d1995662f9b965492ea2261eda83ea8'
            '5b6318f0a74360c048ef910109c9ed2738ad9755d2a3061d341f3c3cb7256d1eedbac2a452d453415e4155720ca51a9abb471a638583d30b7d648483af21b890'
            'f2164b0c31a2f0cc9db0913451ded71c8fc401ac88ebb5cabc00b2902e37c2695b273c75799ca2a060c11555e5b05a2f6aac4c214ec7e000c7b79d90a2e7e07e')

prepare() {
  cd psutil-release-$pkgver
  patch -Np1 -i $srcdir/fix_disk_io_counters.patch
  patch -Np1 -i $srcdir/fix_test_disk_io_counters_sysfs.patch
}

build() {
  cd psutil-release-$pkgver

  python setup.py build --build-lib=build/python

  python2 setup.py build --build-lib=build/python2
  find build/python2 -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
}

check() {
  cd psutil-release-$pkgver

  # Required for testing
  cp setup.py build/python
  cp -r scripts build/python/

# 4 non-working tests in build chroot, requires fixes. Test framework does not allow exclusion.
#  PYTHONPATH="$PWD/build/python" python psutil/tests/__main__.py
}

package_python-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python'
  depends=('python')

  cd psutil-release-$pkgver
  python setup.py build --build-lib=build/python \
                  install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

package_python2-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python2'
  depends=('python2')

  cd psutil-release-$pkgver
  python2 setup.py build --build-lib=build/python2 \
                   install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
