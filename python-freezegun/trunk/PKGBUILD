# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-freezegun
pkgname=('python-freezegun' 'python2-freezegun')
pkgver=0.3.11
pkgrel=1
pkgdesc="Let your Python tests travel through time"
arch=('any')
license=('Apache')
url="https://github.com/spulec/freezegun"
makedepends=('python-setuptools' 'python2-setuptools' 'python-dateutil' 'python2-dateutil')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock'
              'python-coverage' 'python2-coverage' 'python-dateutil' 'python2-dateutil')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/spulec/freezegun/archive/$pkgver.tar.gz")
sha512sums=('6cb5aa96ef400f62fb65d81f38a8a4cfa21f7ed755c071313ea0d6f02dd4aeaf167bcc122fb22aabe99e932af60d8c53a0dfe4d505ddc858ff14828dd98361a0')

prepare() {
  cp -a freezegun-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/freezegun-$pkgver
  python setup.py build

  cd "$srcdir"/freezegun-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/freezegun-$pkgver
  # https://github.com/spulec/freezegun/issues/250
  nosetests3 || warning "Tests failed"

  cd "$srcdir"/freezegun-$pkgver-py2
  nosetests2
}

package_python-freezegun() {
  depends=('python-dateutil')

  cd freezegun-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-freezegun() {
  depends=('python2-dateutil')

  cd freezegun-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
