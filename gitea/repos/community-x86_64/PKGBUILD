# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Frederik Schwan <frederik dot schwan at linux dot com>

pkgname=gitea
pkgver=1.6.0
pkgrel=1
pkgdesc="Painless self-hosted Git service. Community managed fork of Gogs."
arch=('x86_64')
url="https://gitea.io"
license=('MIT')
depends=('git')
makedepends=('go-pie' 'go-bindata')
optdepends=(
  'mariadb: MariaDB support'
  'memcached: MemCached support'
  'openssh: GIT over SSH support'
  'pam: Authentication via PAM support'
  'postgresql: PostgreSQL support'
  'redis: Redis support'
  'sqlite: SQLite support'
)
backup=('etc/gitea/app.ini')
_tag=a60c3a5aa6489523209a388454add00b84178def # tags/1.6.0^0
source=("git+https://github.com/go-gitea/gitea.git#tag=${_tag}"
        gitea.tmpfiles
        gitea-repos.patch
        gitea-ldflags.patch
        gitea-disable-u2f.patch)
sha256sums=('SKIP'
            'f6829347594d424d63bcc211f85effaea90b677960593fe3ca978b3b5a3d12a7'
            '66c5a84fbbe56dd8b3bca3a779aaaf5d4855573f0dd2b273018a8983d7951a72'
            '9357afc653c70f524a4e3c42cc99c1e6bb4ff40a7460391fa283b6fcf3202e7b'
            'd6842efd13b6971b77d233ff3ac2bd3d81c5d26c348ae1f2a2b19b0e7237726e')
# validpgpkeys=('8C4033A23895237CB27D52D9D9B5613BEB813F99') # Matti Ranta <matti@mdranta.net>, retrieved from https://github.com/techknowlogick.gpg

prepare() {
  mkdir -p gopath/src/code.gitea.io
  ln -rTsf ${pkgname} gopath/src/code.gitea.io/${pkgname}

  cd ${pkgname}
  # Change default repos path for ArchLinux
  patch -Np1 -i ../gitea-repos.patch
  # Fix LDFLAGS not being respected by Go
  patch -Np1 -i ../gitea-ldflags.patch
  # https://github.com/go-gitea/gitea/issues/4692
  patch -Np1 -i ../gitea-disable-u2f.patch
}

build() {
  export GOPATH="${srcdir}"/gopath
  cd gopath/src/code.gitea.io/gitea
  make generate
  EXTRA_GOFLAGS="-gcflags all=-trimpath=${GOPATH} -asmflags all=-trimpath=${GOPATH}" \
  make GOFLAGS="-v" TAGS="bindata sqlite pam" build
}

package() {
  install -Dm755 ${pkgname}/${pkgname} -t "${pkgdir}"/usr/bin/
  install -Dm644 ${pkgname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
  install -D ${pkgname}/custom/conf/app.ini.sample "${pkgdir}"/etc/gitea/app.ini
}
