# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

pkgname=dune
pkgver=1.5.1
pkgrel=2
pkgdesc="A composable build system for OCaml (formerly jbuilder)"
arch=('x86_64')
url="https://github.com/ocaml/dune"
license=('Apache')
depends=('glibc' 'ocaml' 'ocaml-findlib')
provides=('jbuilder')
conflicts=('jbuilder')
replaces=('jbuilder')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz")
sha256sums=('fb7fe87ab6c3b4097512a0357629ca1cb9422e0593bd61cc255ab4a1a2aaad50')

build() {
    cd ${pkgname}-${pkgver}
    make release
}

# Tests requires a bunch of (currently) unpackaged dependencies
#check() {
#    cd ${pkgname}-${pkgver/b/-beta}
#    make test
#}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" INSTALL_ARGS="--prefix=/usr --libdir='$(ocamlfind printconf destdir)'" install

    # Fix doc and man install
    rm -r "${pkgdir}"/usr/doc
    install -dm755 "${pkgdir}"/usr/share
    mv "${pkgdir}"/usr/{man,share/}
}
