# Maintainer:  Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>

pkgname=vnstat
pkgver=2.1
pkgrel=1
pkgdesc="A console-based network traffic monitor"
arch=('x86_64')
url="http://humdi.net/vnstat/"
license=('GPL')
depends=('bash' 'sqlite')
makedepends=('gd')
optdepends=('gd: image output')
backup=(etc/vnstat.conf)
source=(http://humdi.net/vnstat/$pkgname-$pkgver.tar.gz
		'vnstat.conf')
sha256sums=('d12cc8deb3d6ecac59a6554efce5df9e80e750f4de2b48b56e66b8f496e9607a'
            '15bb2346cf6382fe13c8aac5b11e02ce5306b743b5a130c97f360b104c1a4384')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  mkdir -p "$pkgdir"/usr/bin
  make DESTDIR="$pkgdir" install
  install -Dm0755 examples/vnstat.cgi "$pkgdir"/usr/share/doc/vnstat/examples/vnstat.cgi
  install -Dm0644 examples/systemd/vnstat.service "$pkgdir"/usr/lib/systemd/system/vnstat.service
  install -dm0755 "$pkgdir"/usr/lib/sysusers.d
  install -Dm0644 "${srcdir}"/vnstat.conf "$pkgdir"/usr/lib/sysusers.d/vnstat.conf
}
