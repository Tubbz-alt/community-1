# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgbase=python-rfc3986
pkgname=(python-rfc3986 python2-rfc3986)
pkgver=1.2.0
pkgrel=1
pkgdesc="Validating URI References per RFC 3986"
arch=('any')
license=('Apache')
url="https://rfc3986.readthedocs.org/"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/r/rfc3986/rfc3986-$pkgver.tar.gz")
sha512sums=('b88bdc47fa2daa6a9ffc339b1b997a85d0c8b3bf3ab271554e2a1a684c0394370e9cba07a82a542f39533a4c8cae69414234545db6caa2508e0ee7e7f8bdff3d')

prepare() {
  cp -a rfc3986-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/rfc3986-$pkgver
  python setup.py build

  cd "$srcdir"/rfc3986-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/rfc3986-$pkgver
  python setup.py pytest

  cd "$srcdir"/rfc3986-$pkgver-py2
  python2 setup.py pytest
}

package_python-rfc3986() {
  depends=('python')

  cd rfc3986-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-rfc3986() {
  depends=('python2')

  cd rfc3986-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
