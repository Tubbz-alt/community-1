# Maintainer: nosu@artixlinux.org

_url="https://gitweb.gentoo.org/repo/gentoo.git/plain/mail-filter/postgrey/files"
_pkg=postgrey
pkgname=postgrey-openrc
pkgver=20181017
pkgrel=1
pkgdesc="OpenRC postgrey init script"
arch=('any')
url="https://gitea.artixlinux.org/packages/postgrey-openrc"
license=('GPL2')
groups=('openrc-galaxy')
depends=('openrc' 'postgrey')
conflicts=('systemd-sysvcompat')
backup=('etc/conf.d/postgrey')
source=("postgrey.confd::${_url}/postgrey.conf.new"
        "postgrey.initd::${_url}/postgrey-1.34-r3.rc.new")
sha256sums=('c4c5bfe68b99513152669443ee268186989a89f9846e7fd560841d4cb47fb031'
            '119ede5d8be2b7b0bf945121c9427946fde73610e447717070d98a2fb42c1961')

pkgver() {
    date +%Y%m%d
}

_inst_initd(){
    install -Dm755 ${srcdir}/$1.initd ${pkgdir}/etc/init.d/$1

    sed -e 's|#!/sbin/openrc-run|#!/usr/bin/openrc-run|g' \
        -e 's|/var/run|/run|g' \
        -e 's|/usr/sbin|/usr/bin|g' \
        -i ${pkgdir}/etc/init.d/$1
}

_inst_confd(){
    install -Dm755 ${srcdir}/$1.confd ${pkgdir}/etc/conf.d/$1
    sed -e 's|/var/run|/run|g' \
        -i "${pkgdir}/etc/conf.d/$1"
}

package() {
    _inst_confd 'postgrey'
    _inst_initd 'postgrey'
}
