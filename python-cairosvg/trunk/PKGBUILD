# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-cairosvg
_pkgname=CairoSVG
pkgver=2.2.1
pkgrel=1
pkgdesc='SVG converter based on Cairo. It can export SVG files to PDF, PostScript and PNG files'
arch=('any')
url='https://cairosvg.org/'
license=('LGPL3')
depends=('python' 'python-setuptools' 'python-cairocffi' 'python-pillow' 'python-cssselect2' 'python-defusedxml' 'python-tinycss2')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-isort')
replaces=('cairosvg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Kozea/CairoSVG/archive/$pkgver.tar.gz"
        'https://github.com/Kozea/CairoSVG/commit/1f403ad229f0e2782d6427a79f0fbeb6b76148b6.patch')

prepare() {
  cd $_pkgname-$pkgver

  cp -r cairosvg test_non_regression/cairosvg_reference/

  # Fix tests
  patch -p1 < "$srcdir"/1f403ad229f0e2782d6427a79f0fbeb6b76148b6.patch
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

sha256sums=('e8068bfb9d86ef4646054fa573de7b6250550bd87f0cb68eb58de43bdf8f2606'
            '726f22550386e7af2a5d7443985ac2ec0c80e20d868069ba432898fed35b001e')
