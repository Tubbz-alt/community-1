# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-restkit
pkgname=('python2-restkit')
pkgver=4.2.2
pkgrel=2
pkgdesc="An HTTP resource kit for Python"
arch=('any')
license=('MIT')
url="http://benoitc.github.com/restkit"
makedepends=('python-setuptools' 'python2-setuptools' 'python-http-parser' 'python2-http-parser'
             'python-socketpool' 'python2-socketpool' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-webob' 'python2-webob')
source=("git+https://github.com/benoitc/restkit.git#tag=$pkgver"
        restkit-disable-tests-needing-internet-connection.patch)
sha512sums=('SKIP'
            '56c597996d3f1bf3e5ddf1b09a0663e531cef290639eba4357dc2873a03fd7f19e5432bd085fcd9e9b97c1443f283eff54628c99f6f4494d4e905d366bc021fd')

prepare() {
  sed -i '/data_files/d' restkit/setup.py
  (cd restkit && patch -p0 -i ../restkit-disable-tests-needing-internet-connection.patch) # Disable tests that rely on broken website (Fedora)
  cp -a restkit{,-py2}
}

build() {
  cd "$srcdir/restkit"
  # python setup.py build

  cd "$srcdir/restkit-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/restkit"
  # python setup.py test

  cd "$srcdir/restkit-py2"
  python2 setup.py test
}

package_python-restkit() {
  depends=('python-socketpool' 'python-http-parser')

  cd restkit
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-restkit() {
  depends=('python2-socketpool' 'python2-http-parser')

  cd restkit-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
